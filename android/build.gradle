buildscript {
    ext.kotlin_version = '2.1.0'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.9.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }

    // 强制 buildscript 依赖也使用统一的 Kotlin 版本
    configurations.classpath {
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'org.jetbrains.kotlin') {
                details.useVersion kotlin_version
            }
        }
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }

    // 强制所有项目的 buildscript 使用统一的 Kotlin 版本
    buildscript {
        configurations.classpath {
            resolutionStrategy.eachDependency { details ->
                if (details.requested.group == 'org.jetbrains.kotlin') {
                    details.useVersion rootProject.ext.kotlin_version
                }
            }
        }
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            if (project.android.namespace == null) {
                project.android.namespace = project.group.toString()
            }
            project.android {
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_17
                    targetCompatibility JavaVersion.VERSION_17
                }
                if (project.android.hasProperty('kotlinOptions')) {
                    project.android.kotlinOptions {
                        jvmTarget = '17'
                    }
                }
            }
        }
    }

    // 强制所有子项目使用相同的 Kotlin 版本
    project.configurations.all {
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'org.jetbrains.kotlin') {
                details.useVersion rootProject.ext.kotlin_version
            }
        }
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
subprojects {
    project.evaluationDependsOn(':app')
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
